    let Monad = ./../Monad/Type

in  let EitherT = ./Type

in  let Either = ./../Either/Type

in  let fold = ./../Either/fold

in    λ(a : Type)
    → λ(m : Type → Type)
    → λ(monad : Monad m)
    →     ./Applicative/Sequential
          a
          m
          { map = monad.map, ap = monad.ap, pure = monad.pure }
        ∧ { bind =
                λ(b : Type)
              → λ(c : Type)
              → λ(k : b → EitherT a m c)
              → monad.bind
                (Either a b)
                (Either a c)
                (   λ(either : Either a b)
                  → fold
                    a
                    b
                    (EitherT a m c)
                    (   λ(l : a)
                      → monad.pure (Either a c) < Left = l | Right : c >
                    )
                    (λ(r : b) → k r)
                    either
                )
          }
      : Monad (EitherT a m)
