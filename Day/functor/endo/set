    let Day = ./../../Type

in    λ(f : Type → Type)
    → λ(g : Type → Type)
    →   { map =
              λ(a : Type)
            → λ(b : Type)
            → λ(fn : a → b)
            → λ(day : Day f g a)
            → day
              (Day f g b)
              (   λ(c : Type)
                → λ(d : Type)
                → λ(e : { _1 : f c, _2 : g d, _3 : c → d → a })
                → λ(r0 : Type)
                → λ(day0 : ./../../convolution f g b r0)
                → day0
                  c
                  d
                  { _1 =
                      e._1
                  , _2 =
                      e._2
                  , _3 =
                      λ(x : c) → λ(y : d) → fn (e._3 x y)
                  }
              )
        }
      : ./../../../Functor/Endo/Set (Day f g)
