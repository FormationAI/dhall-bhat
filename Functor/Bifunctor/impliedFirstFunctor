    let kArrow = ./../../Function/Kind

in  let kProduct = ./../../Tuple/Kind

in    λ(cObject : Kind)
    → λ(cArrow : kArrow (kProduct cObject cObject) Type)
    → λ(dObject : Kind)
    → λ(dArrow : kArrow (kProduct dObject dObject) Type)
    → λ(dCategory : ./../../Category/Type dObject dArrow)
    → λ(eObject : Kind)
    → λ(eArrow : kArrow (kProduct eObject eObject) Type)
    → λ(f : kArrow (kProduct cObject dObject) eObject)
    → λ(bifunctor : ./Type cObject dObject eObject cArrow dArrow eArrow f)
    → λ(a : dObject)
    →   { map =
              λ(b : cObject)
            → λ(c : cObject)
            → λ(fn : cArrow { _1 = b, _2 = c })
            → bifunctor.map
              { _1 = b, _2 = a }
              { _1 = c, _2 = a }
              { _1 = fn, _2 = dCategory.identity a }
        }
      : ./../Type
        cObject
        eObject
        cArrow
        eArrow
        (λ(b : cObject) → f { _1 = b, _2 = a })
