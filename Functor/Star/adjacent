{- If there is a natural transformation from `m` to `n`, then a n-Star functor
   can be made into an m-Star functor.

   NB: This looks a lot like a contravariant functor in the category of
       endofunctors … right?
-}
  λ(object : Kind)
→ λ(arrow : object → object → Type)
→ λ(category : ./../../Category/Type object arrow)
→ λ(m : object → object)
→ λ(n : object → object)
→ λ(natural : ∀(a : object) → arrow (m a) (n a))
→ λ(f : object → object)
→ λ(star : ./Type object arrow n f)
→   { map =
          λ(a : object)
        → λ(b : object)
        → λ(fn : arrow a (m b))
        → star.map
          a
          b
          ( category.op
            a
            (n b)
            (   λ(r : Type)
              → λ(arrowsOut : ∀(z : object) → arrow z (n b) → arrow a z → r)
              → arrowsOut (m b) (natural b) fn
            )
          )
    }
  : ./Type object arrow m f
