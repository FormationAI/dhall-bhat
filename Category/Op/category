    let kArrow = ./../../Function/Kind

in  let kProduct = ./../../Tuple/Kind

in    λ(object : Kind)
    → λ(arrow : kArrow (kProduct object object) Type)
    → λ(category : ./../Type object arrow)
    →     let opArrow = ./Type object arrow
      
      in    { identity =
                category.identity
            , op =
                  λ(a : { _1 : object, _2 : object })
                → λ ( p
                    :   ∀(r : Type)
                      → (∀(z : object) → opArrow z a._2 → opArrow a._1 z → r)
                      → r
                    )
                → category.op
                  { _1 = a._2, _2 = a._1 }
                  (   λ(r : Type)
                    → λ ( arrowsOut
                        :   ∀(z : object)
                          → arrow { _1 = z, _2 = a._1 }
                          → arrow { _1 = a._2, _2 = z }
                          → r
                        )
                    → p
                      r
                      (   λ(z : object)
                        → λ(f : opArrow z a._2)
                        → λ(g : opArrow a._1 z)
                        → arrowsOut z g f
                      )
                  )
            }
          : ./../Type object (./../../uncurryT object object Type opArrow)
