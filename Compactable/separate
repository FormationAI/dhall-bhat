{-

`separate` partitions any `Left` values from `f` into the `_1` side of a tuple, and any `Right` values
from `f` into the `_2` side of a tuple, essentially separating the values.

Examples:

```
    let E = constructors (./Either/Type Natural Natural)

in  ./Compactable/separate
    List
    ./List/compactable
    Natural
    Natural
    [ E.Right 1, E.Left 2, E.Right 3, E.Left 4, E.Right 5 ]
= { _1 = [ 2, 4 ], _2 = [ 1, 3, 5 ] }
```

-}
    let Either = ./../Either/Type

in    λ(f : Type → Type)
    → λ(c : ./Type f)
    → λ(l : Type)
    → λ(r : Type)
    → ./mapEither
      f
      c
      (Either l r)
      l
      r
      ((./../Function/category).identity (Either l r))
